---
layout: post
title: Implementando una Static RAM y un Program Counter - CS03
---

Hoy, el objetivo es implementar una RAM estatica **(SRAM)**, esta RAM no es la tipica tarjeta RAM que normalmente se compra para los computadores ya que una SRAM es mas cara que una Dynamic RAM (DRAM), por lo general las SRAM estan dentro de la CPU.

Para completar los objetivos de este capitulo vamos implementar los siguientes chips:
- Data Flip-Flop (primitive) (Ya implementado)
- 1-bit Register
- 16-bit Register
- 16-bit / 8-register memory	
- 16-bit / 64-register memory
- 16-bit / 512-register memory
- 16-bit / 4096-register memory
- 16-bit / 16384-register memory	
- 16-bit program counter (PC)

Para lograrlo tendremos a nuestra disposicion un Data Flip-Flop que ya ha sido implementado, es decir dicha complejidad se abstrae.

# Tabla de contenidos

- [Tabla de contenidos](#tabla-de-contenidos)
- [Conceptos importantes](#conceptos-importantes)
  * [La grafica de una señal de reloj (concepto1)](#user-content-la-grafica-de-una-señal-de-reloj-concepto1)
  * [Diagramas de tiempo (concepto2)](#user-content-diagramas-de-tiempo-concepto2)
    + [Flip-Flop tipo D](#flip-flop-tipo-d)
    + [Bit](#bit)
    + [Registro](#registro)
    + [Memoria RAM](#memoria-ram)
  * [Logica secuencial vs Logica combinacional (concepto3, concepto4)](#user-content-logica-secuencial-vs-logica-combinacional-concepto3-concepto4)
  * [En la RAM implementada, ¿son las operaciones de escritura secuenciales y las de lectura combinacionales?](#user-content-en-la-ram-implementada-son-las-operaciones-de-escritura-secuenciales-y-las-de-lectura-combinacionales)
  * [Direccion en memoria VS contenido de dicha direccion (concepto6)](#user-content-direccion-en-memoria-vs-contenido-de-dicha-direccion-concepto6)
  * [Calculando en bits el tamaño de una memoria RAM (concepto8)](#user-content-calculando-en-bits-el-tamaño-de-una-memoria-ram-concepto8)
  * [Importancia del flanco de bajada del reloj (concepto9)](#user-content-importancia-del-flanco-de-bajada-del-reloj-concepto9)
  * [Utilidad del program counter en un computador (concepto10)](#user-content-utilidad-del-program-counter-en-un-computador-concepto10)
  * [Los retardos de propagacion (reto)](#user-content-los-retardos-de-propagacion-reto)
    + [Implicaciones para el funcionamiento del un circuito combinacional](#implicaciones-para-el-funcionamiento-del-un-circuito-combinacional)
    + [Implicaciónes para el funcionamiento de un circuito secuencial](#user-content-implicaciónes-para-el-funcionamiento-de-un-circuito-secuencial)
- [Implementando el 1-bit register](#implementando-el-1-bit-register)
- [Implementando el 16-bit register](#implementando-el-16-bit-register)
- [Implementando el 16-bit - 8 register memory](#implementando-el-16-bit---8-register-memory)
- [Implementando el 16-bit - 64 register memory](#implementando-el-16-bit---64-register-memory)
- [Implementando el 16-bit - 512 register memory](#implementando-el-16-bit---512-register-memory)
- [Implementando el 16-bit - 4096 register memory](#implementando-el-16-bit---4096-register-memory)
- [Implementando el 16-bit - 16384 register memory](#implementando-el-16-bit---16384-register-memory)
- [Implementando el 16-bit program counter](#implementando-el-16-bit-program-counter)
- [Codigo fuente](#codigo-fuente)
- [Lista de chequeo y autoevaluacion](#lista-de-chequeo-y-autoevaluacion)
- [Fuentes](#fuentes)

<small><i><a href='http://ecotrust-canada.github.io/markdown-toc/'>Table of contents generated with markdown-toc</a></i></small>

 
# Conceptos importantes
## La grafica de una señal de reloj (concepto1)
![](http://imgur.com/FD627fF.gif)

Como se puede ver en la imagen el tick es la parte de la señal de reloj que vale 0 y el tock es la parte que vale 1, por otra parte el flanco de subida es el momento en el que la señal pasa de 0 a 1, y el flanco de subida el momento en el que la señal pasa de 1 a 0.

En nuestra arquitectura con el flanco de bajada termina un ciclo y empieza el siguiente (Notese que en otras arquitecturas esto puede ser diferente).

Un ciclo tarda un tiempo desde que inicia en el tick hasta que finaliza el tock con un flanco de bajada, este tiempo se conoce como periodo.

> El periodo es el tiempo que tarda un ciclo en completarse.

La cantidad de ciclos que se pueden realizar en un segundo es entonces la frecuencia y estas dos magnitudes se relacionan de la siguiente forma:

![](https://latex.codecogs.com/gif.latex?f=\frac{1}{T})


## Diagramas de tiempo (concepto2)
A continuación se analiza el estado de los pines a travez del tiempo.
### Flip-Flop tipo D
![](http://imgur.com/pTORB23.gif)

### Bit
![](http://imgur.com/GTQKEx8.gif)

- Notese que en ciclo 24 el load(t-1)=1 y el out(t)=0 cuando el out(t-1)=0 porque el in(t-1)=0
- Notese que en el ciclo 28 load(t-1)=1 y el out(t)=1 cuando el out(t-1)=1 porque el in(t-1)=1

### Registro
![](http://imgur.com/uuEagAO.gif)


### Memoria RAM
![](http://imgur.com/i6CAF0G.gif)
 
## Logica secuencial vs Logica combinacional (concepto3, concepto4)
![](http://imgur.com/7XvlsLQ.gif)
El diagrama anterior nos puede ayudar a entender claramente la diferencia entre lógica combinaciónal y lógica secuencial, como se puede ver, la salida de una función de lógica combinacional depende unicamente de su entrada, y esta entrada en ningún caso puede ser la salida puesto que entrariamos en un loop indeterminado.

Por otra parte en un chip de lógica secuencial sa salida en el tiempo t puede depender de in(t-1) o out(t-1), esto es posible debido a que cuando ocurre in(t)=out(t-1) se esta especificando claramente a cual salida nos referimos, cosa que no pasa si nuestro chip no puede ser *"consciente"* del tiempo.

Otro tema importante que se puede explicar con la imagen es que un circuito de logica combinacional se puede usar en conjunto con uno de lógica secuencial, por ejemplo en una RAM8 se puede usar un **demultiplexor** para seleccionar un registro determinado (lógica combinacional) y luego de que el dato ha sido procesado por el registro seleccionado (Register) (lógica secuencial), dicha salida debe ser procesada por un **Multiplexor** para determinar cual de los registros tiene el dato que la salida de la RAM requiere.

Dicho lo anterior podemos enumerar unas diferencias fundamentales entre un circuito lógico combinacional y un circuito lógico secuencial:
1. El circuito secuencial es capaz de memorizar datos pasados mientras el combinacional no.
2. El circuito secuencial es *"consciente"* del tiempo mientras el combinacional no.
3. El circuito secuencial puede ser funcion de in(t-1) o out(t-1) mientras el combinacional al no ser *"consciente"* del tiempo depende unicamente de in.

## En la RAM implementada, ¿son las operaciones de escritura secuenciales y las de lectura combinacionales?
Para detallar mas la pregunta: En relación a las memorias RAM implementadas, ¿se puede decir que las operaciones de escritura son secuenciales y las de lectura son combinacionales?

A esta pregunta mi respuesta es **NO**, porque el hecho de que una operación sea de lectura o escritura no hace que los registros (los cuales son circuitos secuenciales) dejen de usarse, los registros se usan siempre y lo unico que cambia cuando una operación es de lectura o escritura es en funcion de quien queda el circuito: Cuando es de lectura queda en función de out(t-1) y cuando es una operación de escritura queda en función de in(t-1) en ambos casos el circuito debe ser *"consciente"* del tiempo y esta propiedad no la poseen los circuitos combinacionales.

**Por ejemplo**:
Supongamos que tenemos una RAM de 8 registros, y que en el registro 03 en el tiempo 2 hay un 15 almacenado.

Si en el flanco de bajada load es igual a 0 (operación de lectura) en el tiempo 3 el out sera 15, es decir out(t)=out(t-1), lo cual indica claramente que en una operación de lectura la operación depende del tiempo, por lo tanto es un circuito secuencial.

## Direccion en memoria VS contenido de dicha direccion (concepto6)
Son dos conceptos relacionados pero diferentes, si nos referimos a una direccion de memoria nos estamos referiendo a un registro especifico de la memoria RAM, seria como señalar el registro al cual nos referimos con el dedo, en cambio el contenido de una dirección de memoria es la información almacenada en dicho registro, serial algo como leer la información almacenada en el registro señalado anteriormente con el dedo.

NOTA: El contenido de una direción en memoria puede ser a su vez otra dirección en memoria.

## Calculando en bits el tamaño de una memoria RAM (concepto8)
Supongamos que conocemos que el bus de direcciones de nuestra memoria RAM tiene n bits y que cada registro en la memoria tiene un ancho de m bits, ¿Cual es la capacidad de la memoria RAM?

Para responder a esta pregunta primero debemos responder cuantos registros tiene la memoria RAM, esto se puede calcular facilmente con la siguiente ecuación.

![](https://latex.codecogs.com/gif.latex?VR_2^n&space;=&space;2^n)

La cual es la ecuación de la variación con repetecion de un grupo de 2 elementos (true y false) tomados de n en n, en otras palabras esta es la ecuación que nos dice cuantos numeros (en este caso registros) se pueden representar con los n bits del bus de la RAM.

En este punto ya conocemos el numero de registros de nuestra memoria RAM y tambien conocemos el ancho de cada registro (m bits) con esta información sería simplemente cuestion de multiplicar estos dos valores para obtener la capacidad de la memoria RAM en bits.

![](https://latex.codecogs.com/gif.latex?Capacity&space;=&space;2^n*m)

Y la anterior es la formula general en función de n y m para calcular el tamaño en bits de una memoria RAM.

## Importancia del flanco de bajada del reloj (concepto9)
En nuestra arquitectura el flanco de bajada es importante porque es en el momento en el que este ocurre cuando se toma la *"instantanea"* del tiempo t-1, que seran usadas por el circuito secuencial en el tiempo t.

Entiendase *"instantanea"* como el valor de los pines de entrada en el momento en el que ocurre el flanco de bajada, esto quiere decir que dichos valores pueden variar todo lo que quieran antes de que ocurra el flanco de bajada y dicha variación no afectara la entrada en el tiempo t si justo antes de que ocurra el flanco de bajada se deja todo como estaba al principio.

Este funcionamiento es el que ocasiona que cuando cambiemos el bit de **Load** en un registro su efecto no se ve inmediatamente reflejado en la salida, es decir si el cambio ocurrio en el tiempo t, el efecto no se vera en el tiempo t, sino en el tiempo t+1, ademas cabe decir que si el cambio se revierte antes de que ocurra el flanco de bajada es como si no hubiera ocurrido nada en el tiempo t.

**Nota**: No en todas las arquitecturas la *"instantanea"* es tomada en el flanco de bajada.

## Utilidad del program counter en un computador (concepto10)
La utilidad del program counter (PC) en una CPU reside en que con este se puede *"caminar"* las instrucciones a ejecutar, en otras palabras, si lo vieramos desde el punto de vista de un lenguaje de alto nivel, este chip es el que dice que se busque la siguiente linea de codigo, y luego la siguiente y luego la siguiente, en caso de que el programa entre en un alterador de flujo de ejecución (como un while) la ALU activaria el pin LOAD y le pasaria por IN al PC la linea a la que debe ir para seguir con la ejecución del programa.

## Los retardos de propagacion (reto)
Por definicion el retardo de propagación es el tiempo que le toma al Ouput cambiar cuando el Input ha cambiado.

### Implicaciones para el funcionamiento del un circuito combinacional
Este retardo puede producir resultados inesperados en circuitos relativamente complejos, por ejemplo en el Full-adder implementado en el capitulo 1, si tuviesemos que sumar numeros de muchos mas bits la propagación del carry podria en ocasiones no llegar a tiempo a la penultima compuerta y por tanto el circuito supondria que no hay carry, cuando en realidad no es asi.

### Implicaciónes para el funcionamiento de un circuito secuencial
Este retardo implica que el periodo (la duracion de un ciclo) debe ser lo suficientemen larga para que la información tenga tiempo de llegar al punto en el que se requiere, es decir el periodo de un circuito secuencial deberia depender del tiempo de propagación mas largo del circuito completo.

# Implementando el 1-bit register
Su implementación esta explicada en el capitulo 3 de nand2tetris por lo cual no requiere de segunda explicación.

```
/**
 * 1-bit register:
 * If load[t] == 1 then out[t+1] = in[t]
 *                 else out does not change (out[t+1] = out[t])
 */

CHIP Bit {
    IN in, load;
    OUT out;

    PARTS:
    // Put your code here:
    Mux(a=ff1, b=in, sel=load, out=mux1);
    DFF(in=mux1, out=ff1, out=out);
}
```

# Implementando el 16-bit register
En este caso la implementación consiste de un array de 1-bit registers que estan conectados todos al mismo load.

```
/**
 * 16-bit register:
 * If load[t] == 1 then out[t+1] = in[t]
 * else out does not change
 */

CHIP Register {
    IN in[16], load;
    OUT out[16];

    PARTS:
    // Put your code here:
    Bit(in=in[0], load=load, out=out[0]);
    Bit(in=in[1], load=load, out=out[1]);
    Bit(in=in[2], load=load, out=out[2]);
    Bit(in=in[3], load=load, out=out[3]);
    Bit(in=in[4], load=load, out=out[4]);
    Bit(in=in[5], load=load, out=out[5]);
    Bit(in=in[6], load=load, out=out[6]);
    Bit(in=in[7], load=load, out=out[7]);
    Bit(in=in[8], load=load, out=out[8]);
    Bit(in=in[9], load=load, out=out[9]);
    Bit(in=in[10], load=load, out=out[10]);
    Bit(in=in[11], load=load, out=out[11]);
    Bit(in=in[12], load=load, out=out[12]);
    Bit(in=in[13], load=load, out=out[13]);
    Bit(in=in[14], load=load, out=out[14]);
    Bit(in=in[15], load=load, out=out[15]);

}
```

# Implementando el 16-bit - 8 register memory
![](http://imgur.com/fFn29Mr.gif)

Como se ve en el diagrama el LOAD se conecta a un dmux8way, y el address se conecta al selector del dmux, esto con el fin de que envie la operacion de lectura/escritura unicamente al resgistro que indique el address-bus.

Luego la salida de cada registro se envia a un Mux8Way16 cuyo selector se conecta al address-bus de la ram para que seleccione el registro sobre el cual se realizo la instrucción.

```
/**
 * Memory of 8 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

// El addres tiene 3 bits porque log2(8)=3
// load == 1 write
// load == 0 read
CHIP RAM8 {
    IN in[16], load, address[3];
    OUT out[16];

    PARTS:
      // Put your code here:
      DMux8Way(in=load, sel=address, a=loadreg0, b=loadreg1, c=loadreg2, d=loadreg3, e=loadreg4, f=loadreg5, g=loadreg6, h=loadreg7);

      Register(in=in, load=loadreg0, out=reg0);
      Register(in=in, load=loadreg1, out=reg1);
      Register(in=in, load=loadreg2, out=reg2);
      Register(in=in, load=loadreg3, out=reg3);
      Register(in=in, load=loadreg4, out=reg4);
      Register(in=in, load=loadreg5, out=reg5);
      Register(in=in, load=loadreg6, out=reg6);
      Register(in=in, load=loadreg7, out=reg7);

      Mux8Way16(a=reg0, b=reg1, c=reg2, d=reg3, e=reg4, f=reg5, g=reg6, h=reg7, sel=address, out=out);
}
```

# Implementando el 16-bit - 64 register memory
![](http://imgur.com/MIxfNKU.gif)

En un principio se penso en usar la tecnica de los miniterminos para realizar esta implementación pero se observo que seria un trabajo muy tedioso conectar cables a medida que la cantidad de registros de la RAM fuese aumentando, por esta razon se penso en este otro enfoque que es explicado en los comentarios del codigo fuente.


```
// Esta es una ram de 64regx16bits=1024bits
// Esta RAM tiene una capacidad de 128bytes (Ni siquiera es un MegaByte)
/*
Esta RAM debe tener 64 registros cada uno de 16bits.
64reg/8reg = 8. quiere decir que se nesecitan 8 rams
de 8 registros para hacer una RAM de 64 registros

address: tiene 6 bits

xxx yyy

Los 3 bits mas significativos seleccionan una de las 8 rams
los 3 bits menos significativos seleccionan un registro de la ram
seleccionada.

*/
CHIP RAM64 {
    
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux8Way(in=load, sel=address[3..5], a=selram8-0, b=selram8-1, c=selram8-2, d=selram8-3, e=selram8-4, f=selram8-5, g=selram8-6, h=selram8-7);

    // Las 8 RAM8
    RAM8(in=in, load=selram8-0, address=address[0..2], out=ram8-0);
    RAM8(in=in, load=selram8-1, address=address[0..2], out=ram8-1);
    RAM8(in=in, load=selram8-2, address=address[0..2], out=ram8-2);
    RAM8(in=in, load=selram8-3, address=address[0..2], out=ram8-3);
    RAM8(in=in, load=selram8-4, address=address[0..2], out=ram8-4);
    RAM8(in=in, load=selram8-5, address=address[0..2], out=ram8-5);
    RAM8(in=in, load=selram8-6, address=address[0..2], out=ram8-6);
    RAM8(in=in, load=selram8-7, address=address[0..2], out=ram8-7);

    // Retornando la salida
    Mux8Way16(a=ram8-0, b=ram8-1, c=ram8-2, d=ram8-3, e=ram8-4, f=ram8-5, g=ram8-6, h=ram8-7, sel=address[3..5], out=out);

}
```

**Nota**: Todas las RAM siguientes siguen el mismo enfoque por lo tanto no se mostrara diagrama de circuito.


# Implementando el 16-bit - 512 register memory
```
/*
	Esta RAM debe tener 512 registros cada uno de 16bits.
	512reg/64reg = 8. quiere decir que se nesecitan 8 rams
	de 64 registros para hacer una RAM de 512 registros

	address: tiene 9 bits

	xxx yyy yyy

	Los 3 bits mas significativos seleccionan una de las 8 rams
	los 6 bits menos significativos seleccionan un registro de la ram
	seleccionada.

	512reg*16bits=8192bits (8.2Kb)
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux8Way(in=load, sel=address[6..8], a=selram-0, b=selram-1, c=selram-2, d=selram-3, e=selram-4, f=selram-5, g=selram-6, h=selram-7);

    // Las 8 RAM64
    RAM64(in=in, load=selram-0, address=address[0..5], out=ram-0);
    RAM64(in=in, load=selram-1, address=address[0..5], out=ram-1);
    RAM64(in=in, load=selram-2, address=address[0..5], out=ram-2);
    RAM64(in=in, load=selram-3, address=address[0..5], out=ram-3);
    RAM64(in=in, load=selram-4, address=address[0..5], out=ram-4);
    RAM64(in=in, load=selram-5, address=address[0..5], out=ram-5);
    RAM64(in=in, load=selram-6, address=address[0..5], out=ram-6);
    RAM64(in=in, load=selram-7, address=address[0..5], out=ram-7);

    // Retornando la salida
    Mux8Way16(a=ram-0, b=ram-1, c=ram-2, d=ram-3, e=ram-4, f=ram-5, g=ram-6, h=ram-7, sel=address[6..8], out=out);

}
```


# Implementando el 16-bit - 4096 register memory
```
/*
	Esta RAM debe tener 4096 registros cada uno de 16bits.
	4096reg/512reg = 8. quiere decir que se nesecitan 8 rams
	de 512 registros para hacer una RAM de 4096 registros

	address: tiene 12 bits

	xxx yyy yyy yyy

	Los 3 bits mas significativos seleccionan una de las 8 rams
	los 9 bits menos significativos seleccionan un registro de la ram
	seleccionada.

	4096reg*16bits=65536bits (65.5Kb)
 */

CHIP RAM4K {
    IN in[16], load, address[12];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux8Way(in=load, sel=address[9..11], a=selram-0, b=selram-1, c=selram-2, d=selram-3, e=selram-4, f=selram-5, g=selram-6, h=selram-7);

    // Las 8 RAM64
    RAM512(in=in, load=selram-0, address=address[0..8], out=ram-0);
    RAM512(in=in, load=selram-1, address=address[0..8], out=ram-1);
    RAM512(in=in, load=selram-2, address=address[0..8], out=ram-2);
    RAM512(in=in, load=selram-3, address=address[0..8], out=ram-3);
    RAM512(in=in, load=selram-4, address=address[0..8], out=ram-4);
    RAM512(in=in, load=selram-5, address=address[0..8], out=ram-5);
    RAM512(in=in, load=selram-6, address=address[0..8], out=ram-6);
    RAM512(in=in, load=selram-7, address=address[0..8], out=ram-7);

    // Retornando la salida
    Mux8Way16(a=ram-0, b=ram-1, c=ram-2, d=ram-3, e=ram-4, f=ram-5, g=ram-6, h=ram-7, sel=address[9..11], out=out);


}
```


# Implementando el 16-bit - 16384 register memory
```
 /*
	Esta RAM debe tener 16384 registros cada uno de 16bits.
	16384reg/4096reg = 4. quiere decir que se nesecitan 4 rams
	de 4096 registros para hacer una RAM de 16384 registros

	address: tiene 14 bits

	xx yyy yyy yyy yyy

	Los 2 bits mas significativos seleccionan una de las 4 rams
	los 12 bits menos significativos seleccionan un registro de la ram
	seleccionada.

	16384reg*16bits=264144bits (264.1Kb)
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    // Put your code here:
    DMux4Way(in=load, sel=address[12..13], a=selram-0, b=selram-1, c=selram-2, d=selram-3);

	// Las 8 RAM8
    RAM4K(in=in, load=selram-0, address=address[0..11], out=ram-0);
    RAM4K(in=in, load=selram-1, address=address[0..11], out=ram-1);
    RAM4K(in=in, load=selram-2, address=address[0..11], out=ram-2);
    RAM4K(in=in, load=selram-3, address=address[0..11], out=ram-3);

    // Retornando la salida
    Mux4Way16(a=ram-0, b=ram-1, c=ram-2, d=ram-3, sel=address[12..13], out=out);
}
```


# Implementando el 16-bit program counter
![](http://imgur.com/uuHsOcC.gif)

La implementación de este circuito se puede hacer usando multiplexores, un incrementer y un registro para manterner el estado anterior (t-1) del counter.
```
/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:
    Mux16(a=in, b=lastOutIncremented, sel=inc, out=mux1);
    Mux16(a=mux1, b=in, sel=load, out=mux2);
    Mux16(a=mux2, b=false, sel=reset, out=mux3);

    Register(in=mux3, load=true, out=out, out=lastOut);

    Inc16(in=lastOut, out=lastOutIncremented);
}

```


# Codigo fuente
- [("Código fuente") Cableado implementado para los chips del capítulo](https://github.com/jorovipe97/computer_science_code/tree/master/projects/03)

# Lista de chequeo y autoevaluacion
- [Lista de chequeo](https://docs.google.com/spreadsheets/d/16Iuq9kKbZH58CIHMAtu76HwDrgTNl4CU0BMdhCmkvz8/edit?usp=sharing)
- [Autoevaluación](https://docs.google.com/spreadsheets/d/15c_wDRo6ai6SloKjQvwEwxGT4iaYNr6R7ZIBGrXoVvw/edit?usp=sharing)

# Fuentes
- [Propagation delay in digital circuits - Quora](https://www.quora.com/What-is-propagation-delay-in-digital-circuits)
- [Program counter](https://en.wikipedia.org/wiki/Program_counter)
- [Periodo](https://es.wikipedia.org/wiki/Per%C3%ADodo_de_oscilaci%C3%B3n)
- [Frecuencia](https://es.wikipedia.org/wiki/Frecuencia)
- [Static RAM vs Dynamic RAM](https://www.quora.com/What-is-the-difference-between-DRAM-SRAM-and-SDRAM-Which-one-is-the-best-RAM-technology)
- [Nand2tetris chapter 3](http://nand2tetris.org/03.php)
